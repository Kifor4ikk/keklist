//import org.jooq.meta
import org.jooq.meta.kotlin.*
plugins {
    id("java")
    id("nu.studer.jooq") version "10.1.1"
}

group = "ru.kifor"
version = "1.0-SNAPSHOT"

sourceSets {
    main {
        java {
            srcDirs("src/main/java", "src/main/generated")
        }
    }
}

repositories {
    mavenCentral()
}

jooq {
    version.set("3.19.28")  // the default (can be omitted)
    edition.set(nu.studer.gradle.jooq.JooqEdition.OSS)  // the default (can be omitted)
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-security:4.0.0")
    implementation("org.springframework.boot:spring-boot-starter-web:4.0.0")
    implementation("org.springframework.boot:spring-boot-starter-jooq:4.0.0")
    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0")
    implementation("org.postgresql:postgresql:42.7.8")
    implementation("org.slf4j:slf4j-api:2.0.17")
        // https://mvnrepository.com/artifact/org.json/json
    implementation("org.json:json:20250517")
    jooqGenerator("org.postgresql:postgresql:42.7.8")

    implementation("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")


}

buildscript {
    configurations["classpath"].resolutionStrategy.eachDependency {
        if (requested.group.startsWith("org.jooq") && requested.name.startsWith("jooq")) {
            useVersion("3.20.5")
        }
    }
}


tasks.test {
    useJUnitPlatform()
}
// –ú–æ–∂–Ω–æ –ø–æ–ª—É—á–∏—Ç—å –∏–∑ –ø–µ—Ä–º–µ–Ω–Ω—ã—Ö –æ–∫—Ä—É–∂–µ–Ω–∏—è –∏–ª–∏ –∏–∑ —Ñ–∞–π–ª–∞ gradle.properties
// –ü–æ–ª—É—á–µ–Ω–∏–µ —Å–≤–æ–π—Å—Ç–≤: findProperty –≤–æ–∑–≤—Ä–∞—â–∞–µ—Ç Any?, –Ω—É–∂–Ω–æ –ø—Ä–∏–≤–µ—Å—Ç–∏ –∫ String,
// –∏–ª–∏ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å –æ–ø–µ—Ä–∞—Ç–æ—Ä Elvis (?:) –¥–ª—è –∑–∞–¥–∞–Ω–∏—è –∑–Ω–∞—á–µ–Ω–∏—è –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é.
val dbUser: String = project.findProperty("dbUser") as? String
    ?: System.getenv("JOOQ_DB_USER")
    ?: "postgres"

val dbPassword: String = project.findProperty("dbPassword") as? String
    ?: System.getenv("JOOQ_DB_PASSWORD")
    ?: "1"

val dbUrl: String = project.findProperty("dbUrl") as? String
    ?: System.getenv("JOOQ_DB_URL")
    ?: "jdbc:postgresql://127.0.0.1:5432/kekbase"

jooq {
    version.set("3.20.5")
    edition.set(nu.studer.gradle.jooq.JooqEdition.OSS)

    configurations {
        create("main") {
            //–û—Ç–∫–ª—é—á–∞–µ–º –≥–µ–Ω–µ—Ä–∞—Ü–∏—é –ø—Ä–∏ –∫–æ–º–ø–∏–ª—è—Ü–∏–∏, —Ç–∞–∫ –∫–∞–∫ –∫–æ–¥ —Ñ–∏–∫—Å–∏—Ä—É–µ—Ç—Å—è –≤ Git
            generateSchemaSourceOnCompilation.set(false)

            jooqConfiguration {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = "org.postgresql.Driver"
                    url = dbUrl
                    user = dbUser
                    password = dbPassword
                }
                generator {
                    name = "org.jooq.codegen.DefaultGenerator"
                    target {
                        // 1. –£–∫–∞–∑—ã–≤–∞–µ–º –±–∞–∑–æ–≤—ã–π –ø–∞–∫–µ—Ç –¥–ª—è —Å–≥–µ–Ω–µ—Ä–∏—Ä–æ–≤–∞–Ω–Ω—ã—Ö –∫–ª–∞—Å—Å–æ–≤
                        packageName = "ru.kifor.kek" // <--- –£–∫–∞–∂–∏—Ç–µ –≤–∞—à –∫–æ—Ä–Ω–µ–≤–æ–π –ø–∞–∫–µ—Ç
                        // 2. –£–∫–∞–∑—ã–≤–∞–µ–º, —á—Ç–æ –≥–µ–Ω–µ—Ä–∏—Ä–æ–≤–∞—Ç—å –Ω—É–∂–Ω–æ –≤ –ø–∞–ø–∫—É —Å –∏—Å—Ö–æ–¥–Ω–∏–∫–∞–º–∏
                        // (–ü–ª–∞–≥–∏–Ω —Å–æ–∑–¥–∞—Å—Ç –ø—É—Ç—å {directory}/{packageName})
                        directory = "src/main/generated"
                        // WARNING üß® üß® üß® üß® üß® üß® üß® üß® üß® üß® üß® üß®
                        // directory jooq —Å—á–∏—Ç–∞–µ—Ç —Å–≤–æ–∏–º —Ä–∞–±–æ—á–∏–º –∫–∞—Ç–∞–ª–æ–≥–æ–º –∏ –æ—á–∏—â–∞–µ—Ç –µ–≥–æ –ø—Ä–∏ –≥–µ–Ω–µ—Ä–∞—Ü–∏–∏ –∫–æ–¥–∞.
                        // –ø–æ—ç—Ç–æ–º—É —É–∫–∞–∑—ã–≤–∞–µ–º –ø—É—Ç—å –¥–ª—è –±–µ–∑–æ–ø–∞—Å–Ω–æ–π –ø–∞–ø–∫–∏. –ê–∫–∫—É—Ä–∞—Ç–Ω–µ–µ —Å–º–æ—Ç—Ä–∏—Ç–µ –ø—Ä–∏–º–µ—Ä!
                        // –µ—Å–ª–∏ –±—ã directory = "src/main/java" –∞  packageName = "by.itgas.demo1.db.generated"
                        // –≠—Ç–æ –±—ã —É–¥–∞–ª–∏–ª–æ –ø—Ä–æ–µ–∫—Ç
                    }
                    database {
                        name = "org.jooq.meta.postgres.PostgresDatabase"
                        inputSchema = "public"
                        forcedTypes {
                            forcedType {
                                name = "varchar"
                                includeExpression = ".*"
                                includeTypes = "JSONB?"
                            }
                            forcedType {
                                name = "varchar"
                                includeExpression = ".*"
                                includeTypes = "INET"
                            }
                        }
                    }
                    generate {
                        isDeprecated = false
                        isRecords = false
                        isImmutablePojos = false
                        isFluentSetters = false
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

tasks.named<nu.studer.gradle.jooq.JooqGenerate>("generateJooq") { allInputsDeclared.set(true) }